plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "$rootProject.ext.appPackageName"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode rootProject.ext.versionCode
        versionName "$rootProject.ext.versionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    signingConfigs {
        debug {
            storeFile file(rootProject.ext.keyStorePath)
            storePassword "$rootProject.ext.keystorePassword"
            keyAlias "$rootProject.ext.keyAlias"
            keyPassword "$rootProject.ext.keyPassword"
        }
        release {
            storeFile file(rootProject.ext.keyStorePath)
            storePassword "$rootProject.ext.keystorePassword"
            keyAlias "$rootProject.ext.keyAlias"
            keyPassword "$rootProject.ext.keyPassword"
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    implementation "com.citrix.android.sdk:mamsdk:${rootProject.ext.mamSdkVersion}"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

task downloadTool(type: Download, dependsOn: build) {
    mkdir 'tools'
    src 'https://github.com/citrix/citrix-mam-sdks/raw/develop/tools/java/managed-app-utility.jar'
    dest 'tools'
}

task verifyTool(type: Verify, dependsOn: downloadTool) {
    src new File("${projectDir}/tools/managed-app-utility.jar")
    algorithm 'SHA-256'
    checksum "${rootProject.ext.mamToolSha256}"
}

task generateMdx(type: Exec, dependsOn: verifyTool) {
    commandLine 'java', '-jar', "${projectDir}/tools/managed-app-utility.jar", 'wrap',
            '-in',          "$buildDir/outputs/apk/release/${project.name}-release.apk",
            '-out',         "$buildDir/outputs/apk/release/${project.name}.mdx",
            '-appType',     'sdkapp',
            '-storeUrl',    "https://play.google.com/store/apps/details?id=${rootProject.ext.appPackageName}",
            '-keystore',    "${rootProject.ext.keyStorePath}",
            '-storepass',   "${rootProject.ext.keystorePassword}",
            '-keyalias',    "${rootProject.ext.keyAlias}",
            '-keypass',     "${rootProject.ext.keyPassword}"
}

build.finalizedBy generateMdx