// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.32"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// Load keystore properties
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

// Load Citrix MAM SDK properties
def mamsdkPropertiesFile = rootProject.file("mamsdk.properties")
def mamsdkProperties = new Properties()
mamsdkProperties.load(new FileInputStream(mamsdkPropertiesFile))

// Load debug properties
def debugPropertiesFile = rootProject.file("debug.properties")
def debugProperties = new Properties()
if (debugPropertiesFile.exists()) {
    debugProperties.load(new FileInputStream(debugPropertiesFile))
}

ext {
    appPackageName = "com.teramoto.microvpnbrowser"
    if (debugProperties['appPackageName']) {
        appPackageName = debugProperties['appPackageName']
    }
    versionCode = 2
    if (debugProperties['versionCode']) {
        versionCode = debugProperties['versionCode'].toInteger()
        println debugProperties['versionCode']
        println versionCode
    }
    versionName = "1.1"
    if (debugProperties['versionName']) {
        versionName = debugProperties['versionName']
    }
    mamSdkLibraryMaven = mamsdkProperties['mamsdkMavenAbsolutePath']
    mamSdkLibraryTools = mamsdkProperties['mamSdkLibraryTools']
    mamSdkLibraryLibs = mamsdkProperties['mamSdkLibraryLibs']
    mamSdkVersion = mamsdkProperties['mamSdkVersion']
    keyStorePath = keystoreProperties['storeFile']
    keystorePassword = keystoreProperties['storePassword']
    keyAlias = keystoreProperties['keyAlias']
    keyPassword = keystoreProperties['keyPassword']
}

allprojects {
    repositories {
        maven { url "file://${rootProject.ext.mamSdkLibraryMaven}" }
        google()
        jcenter()
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all { kotlinOptions.allWarningsAsErrors = true }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}